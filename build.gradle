plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

bootJar {
	enabled = false  // 루트 프로젝트에서 실행 가능한 JAR을 비활성화
}

// 모든 하위 모듈들에 이 설정을 적용
subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.example'
	version = '0.0.1-SNAPSHOT'

	// 새로운 방식으로 Java 버전 설정
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	// 기본적으로 실행 가능한 JAR을 비활성화하고, 라이브러리 JAR을 활성화
	bootJar {
		enabled = false
	}
	jar {
		enabled = true
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'io.micrometer:micrometer-registry-prometheus'
		testRuntimeOnly 'com.h2database:h2'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}

	test {
		useJUnitPlatform()  // JUnit5 사용 설정
	}
}

allprojects {
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		options.compilerArgs << "-parameters"
	}
}

// API 모듈에 대해 설정을 오버라이드
project(':api') {
	bootJar {
		enabled = true  // API 모듈만 실행 가능한 JAR 생성
	}
	jar {
		enabled = false
	}
}
